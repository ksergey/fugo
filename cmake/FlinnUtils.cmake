include(CMakeParseArguments)

function(FlinnRemoveMatchesFromList)
  set(options)
  set(oneValueArgs)
  set(multiValueArgs MATCHES)
  cmake_parse_arguments(FLINN_PARSED "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  foreach (FLINN_LIST ${FLINN_PARSED_UNPARSED_ARGUMENTS})
    foreach (FLINN_ENTRY ${${FLINN_LIST}})
      foreach (FLINN_MATCH ${FLINN_PARSED_MATCHES})
        if (${FLINN_ENTRY} MATCHES ${FLINN_MATCH})
          list(REMOVE_ITEM ${FLINN_LIST} ${FLINN_ENTRY})
        endif()
      endforeach()
    endforeach()
    set(${FLINN_LIST} ${${FLINN_LIST}} PARENT_SCOPE)
  endforeach()
endfunction()

function(FlinnAddTestsFromSourceList)
  set(options)
  set(oneValueArgs PREFIX)
  set(multiValueArgs LIBS OPTIONS DEFINITIONS)

  cmake_parse_arguments(FLINN_PARSED "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  foreach (FLINN_LIST ${FLINN_PARSED_UNPARSED_ARGUMENTS})
    foreach (FLINN_ENTRY ${${FLINN_LIST}})
      if (${FLINN_ENTRY} MATCHES ".*_test.cpp")
        string(REGEX REPLACE "^.*\/(.*)_test\.cpp$" "\\1" testName "${FLINN_ENTRY}")
        set(testName "${FLINN_PARSED_PREFIX}-${testName}-test")

        add_executable(${testName} ${FLINN_ENTRY})
        target_compile_options(${testName} PRIVATE ${FLINN_PARSED_OPTIONS})
        target_compile_definitions(${testName} PRIVATE ${FLINN_PARSED_DEFINITIONS})
        target_link_libraries(${testName} PRIVATE ${FLINN_PARSED_LIBS})

        add_test(${testName} ${testName})
      endif()
    endforeach()
  endforeach()
endfunction()

function(FlinnAddBenchmarksFromSourceList)
  set(options)
  set(oneValueArgs PREFIX)
  set(multiValueArgs LIBS COMPILE_OPTIONS LINK_OPTIONS DEFINITIONS)

  cmake_parse_arguments(FLINN_PARSED "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  foreach (FLINN_LIST ${FLINN_PARSED_UNPARSED_ARGUMENTS})
    foreach (FLINN_ENTRY ${${FLINN_LIST}})
      if (${FLINN_ENTRY} MATCHES ".*_bm.cpp")
        string(REGEX REPLACE "^.*\/(.*)_bm\.cpp$" "\\1" benchmarkName "${FLINN_ENTRY}")
        set(benchmarkName "${FLINN_PARSED_PREFIX}-${benchmarkName}-bm")

        add_executable(${benchmarkName} ${FLINN_ENTRY})
        target_compile_options(${benchmarkName} PRIVATE ${FLINN_PARSED_COMPILE_OPTIONS})
        target_compile_definitions(${benchmarkName} PRIVATE ${FLINN_PARSED_DEFINITIONS})
        target_link_options(${benchmarkName} PRIVATE ${FLINN_PARSED_LINK_OPTIONS})
        target_link_libraries(${benchmarkName} PRIVATE ${FLINN_PARSED_LIBS})
      endif()
    endforeach()
  endforeach()
endfunction()

function(FlinnExcludeTestsAndBenchmarksFromSourceList FLINN_LIST)
  list(FILTER ${FLINN_LIST} EXCLUDE REGEX ".*_test.cpp")
  list(FILTER ${FLINN_LIST} EXCLUDE REGEX ".*_bm.cpp")
  set(${FLINN_LIST} ${${FLINN_LIST}} PARENT_SCOPE)
endfunction()

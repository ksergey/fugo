include(CMakeParseArguments)

function(FugoRemoveMatchesFromList)
  set(options)
  set(oneValueArgs)
  set(multiValueArgs MATCHES)
  cmake_parse_arguments(FUGO_PARSED "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  foreach (FUGO_LIST ${FUGO_PARSED_UNPARSED_ARGUMENTS})
    foreach (FUGO_ENTRY ${${FUGO_LIST}})
      foreach (FUGO_MATCH ${FUGO_PARSED_MATCHES})
        if (${FUGO_ENTRY} MATCHES ${FUGO_MATCH})
          list(REMOVE_ITEM ${FUGO_LIST} ${FUGO_ENTRY})
        endif()
      endforeach()
    endforeach()
    set(${FUGO_LIST} ${${FUGO_LIST}} PARENT_SCOPE)
  endforeach()
endfunction()

function(FugoAddTestsFromSourceList)
  set(options)
  set(oneValueArgs PREFIX)
  set(multiValueArgs LIBS OPTIONS DEFINITIONS)

  cmake_parse_arguments(FUGO_PARSED "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  foreach (FUGO_LIST ${FUGO_PARSED_UNPARSED_ARGUMENTS})
    foreach (FUGO_ENTRY ${${FUGO_LIST}})
      if (${FUGO_ENTRY} MATCHES ".*_test.cpp")
        string(REGEX REPLACE "^.*\/(.*)_test\.cpp$" "\\1" testName "${FUGO_ENTRY}")
        set(testName "${FUGO_PARSED_PREFIX}-${testName}-test")

        add_executable(${testName} ${FUGO_ENTRY})
        target_compile_options(${testName} PRIVATE ${FUGO_PARSED_OPTIONS})
        target_compile_definitions(${testName} PRIVATE ${FUGO_PARSED_DEFINITIONS})
        target_link_libraries(${testName} PRIVATE ${FUGO_PARSED_LIBS})

        add_test(${testName} ${testName})
      endif()
    endforeach()
  endforeach()
endfunction()

function(FugoAddBenchmarksFromSourceList)
  set(options)
  set(oneValueArgs PREFIX)
  set(multiValueArgs LIBS COMPILE_OPTIONS LINK_OPTIONS DEFINITIONS)

  cmake_parse_arguments(FUGO_PARSED "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  foreach (FUGO_LIST ${FUGO_PARSED_UNPARSED_ARGUMENTS})
    foreach (FUGO_ENTRY ${${FUGO_LIST}})
      if (${FUGO_ENTRY} MATCHES ".*_bm.cpp")
        string(REGEX REPLACE "^.*\/(.*)_bm\.cpp$" "\\1" benchmarkName "${FUGO_ENTRY}")
        set(benchmarkName "${FUGO_PARSED_PREFIX}-${benchmarkName}-bm")

        add_executable(${benchmarkName} ${FUGO_ENTRY})
        target_compile_options(${benchmarkName} PRIVATE ${FUGO_PARSED_COMPILE_OPTIONS})
        target_compile_definitions(${benchmarkName} PRIVATE ${FUGO_PARSED_DEFINITIONS})
        target_link_options(${benchmarkName} PRIVATE ${FUGO_PARSED_LINK_OPTIONS})
        target_link_libraries(${benchmarkName} PRIVATE ${FUGO_PARSED_LIBS})
      endif()
    endforeach()
  endforeach()
endfunction()

function(FugoExcludeTestsAndBenchmarksFromSourceList FUGO_LIST)
  list(FILTER ${FUGO_LIST} EXCLUDE REGEX ".*_test.cpp")
  list(FILTER ${FUGO_LIST} EXCLUDE REGEX ".*_bm.cpp")
  set(${FUGO_LIST} ${${FUGO_LIST}} PARENT_SCOPE)
endfunction()
